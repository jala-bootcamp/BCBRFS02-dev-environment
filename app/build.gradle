plugins {
    id 'groovy'
    id 'org.springframework.boot' version '3.2.3'
    id 'io.spring.dependency-management' version '1.1.4'

    id 'application'
    id 'com.google.cloud.tools.jib' version '3.4.1'
}

group = 'io.ffactory'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-groovy-templates'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.apache.groovy:groovy'

    // This dependency is used by the application.
    implementation 'com.google.guava:guava:33.0.0-jre'

    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'

    // Use the awesome Spock testing and specification framework even with Java
    testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

application {
    // Define the main class for the application.
    mainClass = 'io.ffactory.pokemondemo.App'
}

jib {
    from {
        image = 'amazoncorretto:17.0.10-al2023-headless'
        auth {
            username = dockerhub_user
            password = dockerhub_token
        }
    }
    to {
        image = 'ffactory/pokemondemo'
        auth {
            username = dockerhub_user
            password = dockerhub_token
        }
        tags = ["${image_tag}"]
    }
    container {
        mainClass = 'io.ffactory.pokemondemo.App'
        ports = ['9090']
        format = 'OCI'
    }
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}
